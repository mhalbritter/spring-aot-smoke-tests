pluginManagement {
	includeBuild "gradle-plugins/aot-smoke-test-plugin"
	plugins {
		id "io.spring.javaformat" version "0.0.34"
		id "org.graalvm.buildtools.native" version "0.9.13"
	}
	repositories {
		gradlePluginPortal()
		mavenCentral()
		maven {
			url "https://repo.spring.io/release"
			content {
				includeGroup "io.spring.ge.conventions"
			}
		}
	}
}

buildscript {
	dependencies {
		classpath("org.springframework.boot:spring-boot-gradle-plugin:3.0.0-SNAPSHOT")
	}
	configurations.classpath {
		resolutionStrategy {
			cacheDynamicVersionsFor 24, 'hours'
			cacheChangingModulesFor 24, 'hours'
		}
	}
	repositories {
		gradlePluginPortal()
		mavenCentral()
		maven {
			url "https://repo.spring.io/snapshot"
		}
		maven {
			url 'https://repo.spring.io/milestone'
		}
		maven {
			url "https://repo.spring.io/release"
		}
	}
}

plugins {
	id "com.gradle.enterprise" version "3.10.1"
	id "io.spring.ge.conventions" version "0.0.11"
}

rootProject.name="spring-aot-smoke-tests"

include "aot-smoke-test-support"
include "aot-smoke-test-third-party-hints"

include "actuator-webflux"
include "actuator-webmvc"
include "aspect"
include "async"
include "command-line-runner"
include "conditional"
include "configuration-properties"
include "jdbc"
include "scheduled"
include "security-webflux"
include "security-webmvc"
include "spring-amqp-rabbit"
include "transactional"
include "webflux-netty"
include "webmvc-tomcat"
include "websocket"
include "websocket-stomp"
